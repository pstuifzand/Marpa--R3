#!/usr/bin/env perl

use 5.010;
use strict;
use warnings FATAL => 'all';
use autodie;
use English qw( -no_match_vars );
use IPC::Cmd;

chomp( my $LATEST =
        qx(grep '^[0-9]' cpan/Changes | head -1 | awk '{print \$1}') );

warn "Bumping -> $LATEST\n";

my ( $major, $minor, $micro ) = ( $LATEST =~ m/\A (\d+) [.] (\d\d\d) [_]? (\d\d\d) /xms );
if ( not defined $micro ) {
    die "bump_version Cannot handle new version number: $LATEST";
}
$major += 0;
$micro += 0;
$minor += 0;

my @versioned_files = split /\n/xms, <<'END_OF_LIST';
cpan/html/lib/Marpa/R3/HTML/Callback.pm
cpan/html/lib/Marpa/R3/HTML.pm
cpan/lib/Marpa/R3/Grammar.pm
cpan/lib/Marpa/R3/Scanless.pm
cpan/lib/Marpa/R3/Stuifzand.pm
cpan/lib/Marpa/R3/Internal.pm
cpan/lib/Marpa/R3/Recognizer.pm
cpan/lib/Marpa/R3/Value.pm
cpan/lib/Marpa/R3/Thin/Trace.pm
cpan/lib/Marpa/R3.pm
END_OF_LIST

for my $versioned_file (@versioned_files) {
    say STDERR $versioned_file;
    my $file = do { local ( @ARGV, $/ ) = ($versioned_file); <> };
    $file =~ s/(\$VERSION \s+ =  \s+ ['] ) \d+ [.] \d\d\d [_]? \d\d\d /$1${LATEST}/xms;
    open my $out, '>', $versioned_file;
    print $out $file;
} ## end for my $versioned_file (@versioned_files)

chdir 'cpan';
die qq{"perl Build.PL" failed: $!}
    if not IPC::Cmd::run(
            command => [ $EXECUTABLE_NAME, 'Build.PL' ],
            verbose => 1
    );
die qq{"./Build distmeta" failed: $!}
    if
    not IPC::Cmd::run( command => [ './Build', 'distmeta' ], verbose => 1 );
